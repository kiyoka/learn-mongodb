#!/bin/sh
:; #-*- mode: nendo; syntax: scheme -*-;;
:; exec /usr/local/bin/nendo $0 $*

;; Using this gem
;;   http://rubydoc.info/gems/apachelogregex/0.1.0/frames
(require "apachelogregex")
(require "date")
(use util.list)
(use rfc.json)

(define ap2-format-list
  '(
    ;; format              symbol        need \"...\"
    ("%h"                  hostname      #f)
    ("%l"                  client_id     #f)
    ("%u"                  username      #f)
    ("%t"                  timestamp     #f)
    ("%r"                  request       #t)
    ("%>s"                 result_code   #f)
    ("%b"                  tfrsize       #f)
    ("%{Referer}i"         referer       #t)
    ("%{User-Agent}i"      user_agent    #t)))

(define ap2-format-symbol-hash
  (alist->hash-table
   (map
    (lambda (x)
      (cons
       (first x)
       (second x)))
    ap2-format-list)))

(define ap2-parser
  (let1 combind
      (string-join
       (map
        (lambda (x)
          (if (third x)
              (string-append "\\\"" (first x) "\\\"")
              (first x)))
        ap2-format-list)
       " ")
    (ApacheLogRegex.new combind)))

(define (ap2-parse-combined line)
  (guard (e ((e.is_a? ApacheLogRegex::ParseError)
             (errorf "Error parsing log file: %s" e.message))
            (else
             (raise e)))
    (let ([h1 (ap2-parser.parse! line)]
          [h2 (make-hash-table eq?)])
      (for-each
       (lambda (key)
         (hash-table-put!
          h2
          (hash-table-get ap2-format-symbol-hash key) ;; convert key "%h" style into 'hostname style.
          (hash-table-get h1 key)))
       (h1.keys))
      h2)))

;;(ap2-parse-combined "87.18.183.252 - - [13/Aug/2008:00:50:49 -0700] \"GET /blog/index.xml HTTP/1.1\" 302 527 \"-\" \"Feedreader 3.13 (Powered by Newsbrain)\"")
                    

(define (to-mongo-data h)
  ;;    irb
  ;; irb(main):024:0> dt = DateTime.parse "[09/Oct/2011 06:45:00 +0900]"
  ;; => #<DateTime: 2011-10-09T06:45:00+09:00 ((2455843j,78300s,0n),+32400s,2299161j)>
  ;; irb(main):025:0> dt.to_s
  ;; => "2011-10-09T06:45:00+09:00"
  ;; irb(main):026:0> 
  
  ;;    mongodb shell
  ;; > ISODate( "2011-10-09T06:45:00+09:00" );
  ;; ISODate("2011-10-08T21:45:00Z")
  
  (let* ([_    (hash-table-get h 'timestamp)]
         [_    (_.sub #/[:]/ " ")]
         [dt   (DateTime.parse _)])
    (hash-table-put!
     h
     'timestamp
     (alist->hash-table
      `(
        ("$date" . ,(* (dt.to_time.to_i) 1000) ;; milisecond
         ))))
    (hash-table-put!
     h
     'tfrsize
     (to-i (hash-table-get h 'tfrsize))))
  h)

;;(to-mongo-data
;; (alist->hash-table
;;  '(
;;    (timestamp . "[09/Oct/2011:06:45:00 +0900]")
;;    (a . b)
;;    (tfrsize . "128"))))


(define (main argv)
  (unwind-protect
      (for-each
       (lambda (line)
         (print
          (construct-json-string
           (to-mongo-data
            (ap2-parse-combined line)))))
       STDIN)
    #f))
